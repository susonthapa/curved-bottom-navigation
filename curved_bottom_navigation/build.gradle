plugins {
    id "com.android.library"
    id "kotlin-android"
    id "com.vanniktech.maven.publish" version "0.31.0"
}
import com.vanniktech.maven.publish.SonatypeHost

ext {
    PUBLISH_GROUP_ID = 'np.com.susanthapa'
    PUBLISH_VERSION = '0.7.0'
    PUBLISH_ARTIFACT_ID = 'curved_bottom_navigation'
}

android {
    compileSdk 34
    namespace "np.com.susanthapa.curvedbottomnavigation"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // to prevent the addition of jvm-target
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    tasks.withType(Javadoc).configureEach {
        enabled = false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    coordinates(PUBLISH_GROUP_ID, PUBLISH_ARTIFACT_ID, PUBLISH_VERSION)
    pom {
        name = PUBLISH_ARTIFACT_ID
        description = 'Curved (cut out) bottom navigation implementation in android with animation and AnimatedVectorDrawable support.'
        url = 'https://github.com/susonthapa/curved-bottom-navigation'
        licenses {
            license {
                name = 'The Apache Software License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id = 'susonthapa'
                name = 'Susan Thapa'
                email = 'susanthapa202@gmail.com'
            }
        }

        // Version control info - if you're using GitHub, follow the
        // format as seen here
        scm {
            connection = 'scm:git:github.com/susonthapa/curved-bottom-navigation.git'
            developerConnection = 'scm:git:ssh://github.com/susonthapa/curved-bottom-navigation.git'
            url = 'https://github.com/susonthapa/curved-bottom-navigation/tree/master'
        }
    }
}